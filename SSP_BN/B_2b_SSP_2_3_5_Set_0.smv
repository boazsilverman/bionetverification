--Auto Subset Sum [2, 3, 5]
-------------------------------
MODULE main
VAR
	row: 0..10;
	column: 0..10;
	junction: {pass, split};
	dir: {dwn, diag};
	flag: boolean;
	sum: 0..11;
	xsum: 0..11;
	tag: array 0..9 of boolean;

ASSIGN
	init(row) := 0;
	init(column) := 0;
	init(junction) := split;
	init(dir) := dwn;
	init(flag) := FALSE;
	init(sum) := 11;
	init(xsum) := 11;

	init(tag[0]) := FALSE;		init(tag[1]) := FALSE;		init(tag[2]) := FALSE;		init(tag[3]) := FALSE;		init(tag[4]) := FALSE;
	init(tag[5]) := FALSE;		init(tag[6]) := FALSE;		init(tag[7]) := FALSE;		init(tag[8]) := FALSE;		init(tag[9]) := FALSE;


	--Always advance to next row
	next(row) := (row + 1) mod 11;

	--Flag turns on when row is 10
	next(flag) := (next(row) = 10 ? TRUE : FALSE);

	--Split junctions at rows 0, 2, 5
	next(junction) :=
					case
						((next(row) = 0)|(next(row) = 2)|(next(row) = 5)): split;
						TRUE: pass;
					esac;

	--Decide direction for next move by to current junction
	next(dir) :=
					case
						((row = 3) & (column = 1)): dwn;
						(junction = split): {dwn, diag};
						(junction = pass): dir;
						TRUE: {dwn, diag};
					esac;

	--If diag, increase column, otherwise dwn, same column
	next(column) :=
					case
						(next(row) = 0): 0;
						(next(dir) = diag): (column + 1) mod 11;
						(next(dir) = dwn): column;
						TRUE: column;
					esac;

	--Pick random sum and xsum after initial state
	next(sum) := (sum = 11 ? {0, 2, 3, 5, 7, 8, 10} : sum);
	next(xsum) := (xsum = 11 ? {1, 4, 6, 9} : xsum);
	--Set tag TRUE if curr row = split, dir = diag
	next(tag[0]) :=
					case
						(row = 0) & (column = 0) & next(dir) = diag: TRUE;
						(next(row) = 0): FALSE;
						TRUE: tag[0];
					esac;

	next(tag[1]) :=
					case
						(row = 2) & (column = 0) & next(dir) = diag: TRUE;
						(next(row) = 0): FALSE;
						TRUE: tag[1];
					esac;

	next(tag[2]) :=
					case
						(row = 2) & (column = 1) & next(dir) = diag: TRUE;
						(next(row) = 0): FALSE;
						TRUE: tag[2];
					esac;

	next(tag[3]) :=
					case
						(row = 2) & (column = 2) & next(dir) = diag: TRUE;
						(next(row) = 0): FALSE;
						TRUE: tag[3];
					esac;

	next(tag[4]) :=
					case
						(row = 5) & (column = 0) & next(dir) = diag: TRUE;
						(next(row) = 0): FALSE;
						TRUE: tag[4];
					esac;

	next(tag[5]) :=
					case
						(row = 5) & (column = 1) & next(dir) = diag: TRUE;
						(next(row) = 0): FALSE;
						TRUE: tag[5];
					esac;

	next(tag[6]) :=
					case
						(row = 5) & (column = 2) & next(dir) = diag: TRUE;
						(next(row) = 0): FALSE;
						TRUE: tag[6];
					esac;

	next(tag[7]) :=
					case
						(row = 5) & (column = 3) & next(dir) = diag: TRUE;
						(next(row) = 0): FALSE;
						TRUE: tag[7];
					esac;

	next(tag[8]) :=
					case
						(row = 5) & (column = 4) & next(dir) = diag: TRUE;
						(next(row) = 0): FALSE;
						TRUE: tag[8];
					esac;

	next(tag[9]) :=
					case
						(row = 5) & (column = 5) & next(dir) = diag: TRUE;
						(next(row) = 0): FALSE;
						TRUE: tag[9];
					esac;

--Valid Network:	Spec returns true, the network always exits on a valid sum
--Invalid Network:	Spec returns false, there exists a non-reachable valid sum. Counter-example shows one non-reachable valid sum
CTLSPEC	NAME	csum := !(EX (AG ((flag = FALSE) | (!(column = sum)))));
--Valid Network:	Spec returns true, there exists no path to an invalid sum
--Invalid Network:	Spec returns false, there exists a path to an invalid sum. Counter-example shows one reachable invalid sum
CTLSPEC	NAME	nsum := !(EF ((flag = TRUE) & (column = xsum)));
